buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://files.minecraftforge.net/maven' }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}
plugins {
    id "com.matthewprenger.cursegradle" version "1.0.7"
    id "se.bjurr.gitchangelog.git-changelog-gradle-plugin" version "1.32"
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

def versionInfo = getGitVersion()
version = "${versionInfo['simpleretrogen.version']}"
group= "cpw.mods" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "simpleretrogen"

minecraft {
    mappings channel: "snapshot", version: "20180814-1.12"
    //runDir = "run"
}

dependencies {
    minecraft "net.minecraftforge:forge:1.12.2-14.23.5.2860"
}

curseforge {
    apiKey = project.hasProperty('curseforge_apikey') ? project.curseforge_apikey : '0'
    project {
        id = '240566'
        changelog = file('CHANGELOG.html')
        changelogType = 'html'
        releaseType = 'beta'
    }
}

afterEvaluate {
    tasks.curseforge240566.dependsOn.remove(reobfJar)
    tasks.curseforge240566.dependsOn.remove(jar)
    tasks.curseforge240566.dependsOn.add(makeChangelog)
}

task makeChangelog(type: se.bjurr.gitchangelog.plugin.gradle.GitChangelogTask) {
    filePath = "CHANGELOG.html"
    untaggedName = "Current release ${project.version}"
//    gitHubApi = "https://api.github.com/repos/cpw/simpleretrogen"
    templateContent = """
<h1>Simpleretrogen changelog history</h1>
<h2>Version ${project.version} for minecraft ${project.minecraft_version}</h2>
<ul>
{{#tags}}
<li><h3>{{name}}</h3>
<hr/>
<ul>
{{#commits}}
<li>{{{message}}}</li>
{{/commits}}
</ul>
{{/tags}}
</ul>
    """
}


jar {
    appendix = project.minecraft_version
}

processResources
{
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft_version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
    doLast {
        def propsFile = new File(destinationDir, 'version.properties')
        def properties = new Properties()
        properties.putAll(versionInfo)
        properties['simpleretrogen.build.mcversion'] = project.minecraft_version
        properties.store(propsFile.newWriter(), null)
    }
}


uploadArchives {
    repositories.mavenDeployer {
        dependsOn 'build'

        if (project.hasProperty('forgeMavenPassword'))
        {
            repository(url: "http://files.minecraftforge.net/maven/manage/upload") {
                authentication(userName: project.getProperty('forgeMavenUser'), password: project.getProperty('forgeMavenPassword')) // the elvis operator. look it up.
            }
        }
        else
        {
            // local repo folder. Might wanna juset use  gradle install   if you wanans end it to maven-local
            repository(url: 'file://localhost/' + project.file('repo').getAbsolutePath())
        }
        // This is just the pom data for the maven repo
        pom {
            groupId = project.group
            // Force the maven upload to use the <mcversion>-<version> syntax preferred at files
            version = "${project.minecraft_version}-${project.version}"
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'simpleretrogen'
                url 'https://github.com/cpw/simpleretrogen'

                scm {
                    url 'https://github.com/cpw/simpleretrogen'
                    connection 'scm:git:git://github.com/cpw/simpleretrogen.git'
                    developerConnection 'scm:git:git@github.com:cpw/simpleretrogen.git'
                }

                issueManagement {
                    system 'github'
                    url 'https://github.com/cpw/simpleretrogen/issues'
                }

                licenses {
                    license {
                        name 'GNU Public License (GPL), Version 3.0'
                        url 'http://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'cpw'
                        name 'cpw'
                        roles { role 'developer' }
                    }
                }
            }
        }
    }
}

// This is a special task for pulling the version information from git and the environment (for BUILD_NUMBER)
def getGitVersion()
{
    def out = [:]

    // call git command.
    def outStream = new ByteArrayOutputStream()
    def result = exec {
        executable = 'git'
        args = [ 'describe', '--long', "--match=[^(jenkins)]*"]
        standardOutput = outStream
    }

    def fullVersion = outStream.toString().trim()
    def matcher = fullVersion =~ /(\d+).(\d+)-(\d+)-(.*)/

    def maj = matcher[0][1]
    def min = matcher[0][2]
    def rev = matcher[0][3]
    def bn = project.ext.properties.buildNumber ?: System.getenv("PROMOTED_NUMBER") ?: System.getenv("BUILD_NUMBER") ?: "1"

    out['simpleretrogen.build.major.number'] = maj.toString()
    out['simpleretrogen.build.minor.number'] = min.toString()
    out['simpleretrogen.build.revision.number'] = rev.toString()
    out['simpleretrogen.build.githash'] = matcher[0][4].toString()
    out['simpleretrogen.build.number' ] = bn.toString()
    out['simpleretrogen.version' ] = "${maj}.${min}.${rev}+${bn}".toString()

    return out
}

